buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'

version = 'v1.0.1-mc1.12.2'
group = 'git.jbredwards.subaquatic'
archivesBaseName = 'Subaquatic'
sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false
}

dependencies {
    //required
    deobfCompile "com.cleanroommc:assetmover:${assetmover_version}"
    deobfCompile "com.github.jbredwards:fluidlogged-api:${fluidlogged_api_version}"

    //optional
    deobfCompile "mezz.jei:jei_1.12.2:${jei_version}"
    deobfProvided 'curse.maven:biomes-o-plenty-220318:2842510'
    deobfProvided 'curse.maven:inspirations-284007:2843007'
    deobfProvided "curse.maven:mantle-74924:2713386:"
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://cursemaven.com' }
    maven { url 'https://maven.cleanroommc.com' }
    maven {
        //location of the maven that hosts JEI files
        name = 'Progwml6 maven'
        url = 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        //location of a maven mirror for JEI files, as a fallback
        name = 'ModMaven'
        url = 'modmaven.k-4u.nl'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//for applying asm outside dev environment
jar {
    manifest {
        attributes(
                'FMLCorePlugin': 'git.jbredwards.subaquatic.mod.asm.ASMHandler',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLAT': 'subaquatic_at.cfg'
        )
    }
}

//for applying asm with intellij
runClient { jvmArgs '-Dfml.coreMods.load=git.jbredwards.subaquatic.mod.asm.ASMHandler' }
runServer { jvmArgs '-Dfml.coreMods.load=git.jbredwards.subaquatic.mod.asm.ASMHandler' }